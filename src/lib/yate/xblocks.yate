module "xblocks"

external scalar xb-escape(scalar)
external boolean xb-modernizr(scalar)
external boolean xb-inarray(scalar, array)
external boolean xb-empty-array(array)
external array xb-concat-array(array, array)
external scalar xb-prefix-rm(scalar, scalar)

match .* xb-core-attrs (array contains, ignore, prefix) {
    if (xb-empty-array(contains)) {
        apply . xb-core-attrs__common (prefix)

    } else if (ignore) {
        apply .[!xb-inarray(name(), contains)] xb-core-attrs__common (prefix)

    } else {
        apply .[xb-inarray(name(), contains)] xb-core-attrs__common (prefix)
    }
}

match .* xb-core-attrs__common (prefix) {
    value = scalar(.)
    name = xb-prefix-rm(name(), prefix)
    boolAttrs = [
        'disabled'
        'multiple'
        'checked'
        'required'
        'readonly'
        'selected'
    ]

    if (xb-inarray(name, boolAttrs)) {
        if (value == true()) {
            @{name} = "{name}"
        }

    } else if (name == 'class') {
        @class += (
            ' '
            value
        )

    } else {
        @{name} = value
    }
}

match .* xb-core-content (escape = '') {
    <content>
        if (!xb-modernizr('createshadowroot')) {
            if (boolean(escape)) {
                .content
            } else {
                html(.content)
            }
        }
    </content>
}
