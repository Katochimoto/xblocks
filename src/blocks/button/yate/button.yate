module "xb-button"
import "xblocks"

match .* attrs {
    buttonAttrsIgnore = [
        'xb-flying'
        'xb-checked'
        'xb-size'
        'xb-theme'
        'xb-ico'
    ]

    apply . xb-core-attrs (buttonAttrsIgnore, true())

    if (name() == 'disabled' && boolean(.)) {
        @class += " nb-button_disabled"
    }

    if (name() == 'xb-flying' && boolean(.)) {
        @class += " nb-button_flying"
    }

    if (name() == 'xb-checked' && boolean(.)) {
        @class += " nb-button_checked"
    }

    if (name() == 'xb-size') {
        @class += " nb-button_size_{.}"
    }

    if (name() == 'xb-theme') {
        @class += " nb-button_theme_{.}"
    }
}

match / xb-button {
<xb-button>
    apply .attrs.* xb-core-attrs
    .content
</xb-button>
}

match /[!.attrs.type || .attrs.type == 'button' || .attrs.type == 'submit'] template {
<button class="nb-button">
    apply .attrs.* attrs

    <span class="nb-button__text">
        apply . xb-core-content
    </span>
</button>
}

match /[.attrs.href] template {
<a class="nb-button">
    apply .attrs.* attrs

    <span class="nb-button__text">
        apply . xb-core-content
    </span>
</a>
}

match /[.attrs.type == 'file'] template {
buttonAttrs = [
    'name'
    'multiple'
    'disabled'
]

<label class="nb-button nb-button_type_attach">
    apply .attrs.* attrs

    <span class="nb-file-intruder">
        <span class="nb-file-intruder__inner">
            <input class="nb-file-intruder__input" type="file">
                apply . xb-core-attrs (buttonAttrs)
            </input>
            <span class="nb-file-intruder__focus"></span>
        </span>
    </span>
    <span class="nb-button__text">
        apply . xb-core-content
    </span>
</label>
}
