module "xb-button"
import "xblocks"

match .* block-attrs {
    attrsIgnore = [
        'xb-flying'
        'xb-checked'
        'xb-size'
        'xb-theme'
        'xb-ico'
    ]

    apply . xb-core-attrs (attrsIgnore, true())

    if (name() == 'disabled' && boolean(.)) {
        @class += " xb-button_disabled"
    }

    if (name() == 'xb-flying' && boolean(.)) {
        @class += " xb-button_flying"
    }

    if (name() == 'xb-checked' && boolean(.)) {
        @class += " xb-button_checked"
    }

    if (name() == 'xb-size') {
        @class += " xb-button_size_{.}"
    }

    if (name() == 'xb-theme') {
        @class += " xb-button_theme_{.}"
    }
}

match / xb-button {
<xb-button>
    apply .attrs.* xb-core-attrs
    .content
</xb-button>
}

match /[!.attrs.type || .attrs.type == 'button' || .attrs.type == 'submit'] template {
<button class="xb-button">
    apply .attrs.* block-attrs

    <span class="xb-button__text">
        apply . xb-core-content
    </span>
</button>
}

match /[.attrs.href] template {
<a class="xb-button">
    apply .attrs.* block-attrs

    <span class="xb-button__text">
        apply . xb-core-content
    </span>
</a>
}

match /[.attrs.type == 'file'] template {
controllerAttrs = [
    'name'
    'multiple'
    'disabled'
]

<label class="xb-button xb-button_type_attach">
    apply .attrs.* block-attrs

    <span class="nb-file-intruder">
        <span class="nb-file-intruder__inner">
            <input class="nb-file-intruder__input" type="file">
                apply . xb-core-attrs (controllerAttrs)
            </input>
            <span class="nb-file-intruder__focus"></span>
        </span>
    </span>
    <span class="xb-button__text">
        apply . xb-core-content
    </span>
</label>
}
