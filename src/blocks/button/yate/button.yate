module "xb-button"
import "xblocks"

match .* block-class {
    if (name() == 'disabled' && boolean(.)) {
        @class += " xb-button_disabled"
    }

    if (name() == 'flying' && boolean(.)) {
        @class += " xb-button_flying"
    }

    if (name() == 'checked' && boolean(.)) {
        @class += " xb-button_checked"
    }

    if (name() == 'size') {
        @class += " xb-button_size_{.}"
    }

    if (name() == 'theme') {
        @class += " xb-button_theme_{.}"
    }
}

match / xb-button {
<xb-button>
    apply .attrs.* xb-core-attrs
    .content
</xb-button>
}

match /[!.attrs.type || .attrs.type == 'button' || .attrs.type == 'submit'] template {
controllerAttrs = [
    'name'
    'disabled'
]
<button class="xb-button">
    apply .attrs.* block-class
    apply .attrs.* xb-core-attrs (controllerAttrs)

    <span class="xb-button__text">
        apply . template-content
    </span>
</button>
}

match /[.attrs.href] template {
controllerAttrs = [
    'name'
]
<a class="xb-button">
    apply .attrs.* block-class
    apply .attrs.* xb-core-attrs (controllerAttrs)

    <span class="xb-button__text">
        apply . template-content
    </span>
</a>
}

match /[.attrs.type == 'file'] template {
controllerAttrs = [
    'name'
    'multiple'
    'disabled'
]

<label class="xb-button xb-button_type_attach">
    apply .attrs.* block-class

    <span class="nb-file-intruder">
        <span class="nb-file-intruder__inner">
            <input class="nb-file-intruder__input" type="file">
                apply .attrs.* xb-core-attrs (controllerAttrs)
            </input>
            <span class="nb-file-intruder__focus"></span>
        </span>
    </span>
    <span class="xb-button__text">
        apply . template-content
    </span>
</label>
}


match .* template-content {
    apply .attrs.ico template-ico
    apply . xb-core-content
}

match .attrs.ico template-ico {
<xb-ico class="xb-ico_float_{.attrs.float}">
    apply .attrs.* xb-core-attrs
    .content
</xb-ico>
}
