module "xb-field"
import "xblocks"

match .* block-class {
    if (name() == 'disabled' && boolean(.)) {
        @class += " is-disabled"
    }

    if (name() == 'autosize' && boolean(.)) {
        @class += " xb-field_autosize"
    }

    if (name() == 'size') {
        @class += " xb-field_size_{.}"
    }
}

match / xb-field {
<xb-field>
    apply .attrs.* xb-core-attrs
</xb-field>
}

match /[.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label || .attrs.autosize] template {
<label class="xb-field_complex">
    apply .attrs.* block-class

    if (.attrs.label) {
        <xb-link href="#" type="input">
            apply .attrs.label.attrs.* xb-core-attrs
            .attrs.label.content
        </xb-link>
    }

    if (.attrs.prefix) {
        <span class="xb-field_complex__left">
            .attrs.prefix
        </span>
    }

    if (.attrs.postfix) {
        <span class="xb-field_complex__right">
            .attrs.postfix
        </span>
    }

    if (.attrs.reset) {
        <xb-ico class="xb-field_complex__reset js-reset"
                type="remove"
                active="true"></xb-ico>
    }

    <span class="xb-field_complex__content">
        apply . template-controller ('xb-field_complex__controller', false())
        <span class="xb-field_complex__view">&nbsp;</span>
    </span>
</label>
}

match /[!(.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label || .attrs.autosize)] template {

    apply . template-controller ('xb-field_simple', true())

}

match . template-controller (class, is-complex) {
controllerAttrs = [
    'name'
    'disabled'
    'placeholder'
    'readonly'
]

if (.attrs.multiline) {
    <textarea>
        @class = (
            class
        )
        if (is-complex) {
            apply .attrs.* block-class
        }
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'rows'
            'cols'
        ]))
        .attrs.value
    </textarea>
} else {
    <input type="text">
        @class = (
            class
        )
        if (is-complex) {
            apply .attrs.* block-class
        }
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'value'
        ]))
    </input>
}
}
