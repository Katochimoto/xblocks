module "xb-field"
import "xblocks"

match .* block-attrs {
    if (name() == 'disabled' && boolean(.)) {
        @class += " is-disabled"
    }

    if (name() == 'size') {
        @class += " xb-field_size_{.}"
    }
}

match / xb-field {
<xb-field>
    apply .attrs.* xb-core-attrs
</xb-field>
}

match /[.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label] template {
controllerAttrs = [
    'name'
    'disabled'
]

<label class="xb-field_complex">
    apply .attrs.* block-attrs

    if (.attrs.label) {
        <a class="xb-field_complex__label" href="#">
            apply .attrs.* xb-core-attrs ([
                'label-href'
                'label-target'
            ], false(), 'label-')

            .attrs.label
        </a>
    }

    if (.attrs.prefix) {
        <span class="xb-field_complex__left">
            .attrs.prefix
        </span>
    }

    if (.attrs.postfix) {
        <span class="xb-field_complex__right">
            .attrs.postfix
        </span>
    }

    if (.attrs.reset) {
        <span class="xb-field_complex__reset js-reset"></span>
    }

    <span class="xb-field_complex__content">
        if (.attrs.multiline) {
            <textarea class="xb-field_complex__controller">
                apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
                    'rows'
                    'cols'
                ]))
                .attrs.value
            </textarea>
        } else {
            <input class="xb-field_complex__controller" type="text">
                apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
                    'type'
                    'value'
                ]))
            </input>
        }
        <span class="xb-field_complex__view">&nbsp;</span>
    </span>
</label>
}

match /[!(.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label)] template {
controllerAttrs = [
    'name'
    'disabled'
]
if (.attrs.multiline) {
    <textarea class="xb-field_simple">
        apply .attrs.* block-attrs
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'rows'
            'cols'
        ]))
        .attrs.value
    </textarea>
} else {
    <input class="xb-field_simple" type="text">
        apply .attrs.* block-attrs
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'type'
            'value'
        ]))
    </input>
}
}
