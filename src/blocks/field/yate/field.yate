module "xb-field"
import "xblocks"

match .* block-attrs (array ignore) {
    attrsIgnore = [
        'xb-size'
        'xb-reset'
        'xb-prefix'
        'xb-postfix'
    ]

    apply . xb-core-attrs (xb-concat-array(attrsIgnore, ignore), true())

    if (name() == 'disabled' && boolean(.)) {
        @class += " is-disabled"
    }

    if (name() == 'xb-size') {
        @class += " xb-field_size_{.}"
    }
}

match / xb-field {
<xb-field>
    apply .attrs.* xb-core-attrs
    .content
</xb-field>
}

match /[.attrs.xb-postfix || .attrs.xb-prefix || .attrs.xb-reset] template {
controllerAttrs = [
    'name'
    'value'
    'disabled'
    'type'
]

<label class="xb-field_complex">
    apply .attrs.* block-attrs (controllerAttrs)

    if (.attrs.xb-prefix) {
        <span class="xb-field_complex__left">
            .attrs.xb-prefix
        </span>
    }

    if (.attrs.xb-postfix) {
        <span class="xb-field_complex__right">
            .attrs.xb-postfix
        </span>
    }

    if (.attrs.xb-reset) {
        <span class="xb-field_complex__reset js-reset"></span>
    }

    <span class="xb-field_complex__content">
        <input class="xb-field_complex__controller" type="text">
            apply .attrs.* xb-core-attrs (controllerAttrs)
        </input>
        <span class="xb-field_complex__view">&nbsp;</span>
    </span>
</label>
}

match /[!(.attrs.xb-postfix || .attrs.xb-prefix || .attrs.xb-reset)] template {
if (.attrs.rows || .attrs.cols) {
    <textarea class="xb-field_simple">
        apply .attrs.* block-attrs
        .content
    </textarea>
} else {
    <input class="xb-field_simple" type="text">
        apply .attrs.* block-attrs
    </input>
}
}
