module "xb-field"
import "xblocks"

match .* attrs (array contains, reverse) {
    if (name() == 'disabled' && boolean(.)) {
        @class += " is-disabled"
    }

    if (name() == 'xb-size') {
        @class += " nb-input_size_{.}"
    }
}

match / xb-field {
<xb-field>
    apply .attrs.* xb-core-attrs
    .content
</xb-field>
}

match /[.attrs.xb-postfix || .attrs.xb-prefix || .attrs.xb-reset] template {
controllerAttrs = [
    'name'
    'value'
    'disabled'
    'type'
]

<label class="xb-field_complex">
    apply .attrs.* attrs

    if (.attrs.xb-prefix) {
        <span class="field-left">
            .attrs.xb-prefix
        </span>
    }

    if (.attrs.xb-postfix) {
        <span class="field-right">
            .attrs.xb-postfix
        </span>
    }

    if (.attrs.xb-reset) {
        <span class="xb-field_complex__reset js-reset"></span>
    }

    <span class="xb-field_complex__content">
        <input class="xb-field_complex__controller">
            apply .attrs.* xb-core-attrs (controllerAttrs)
        </input>
        <span class="xb-field_complex__view">&nbsp;</span>
    </span>
</label>
}

match /[!(.attrs.xb-postfix || .attrs.xb-prefix || .attrs.xb-reset)] template {
    if (.attrs.rows || .attrs.cols) {
        <textarea class="nb-input nb-input_type_simple">
            apply .attrs.* attrs
            .content
        </textarea>
    } else {
        <input class="nb-input nb-input_type_simple" type="text">
            apply .attrs.* attrs
        </input>
    }
}
