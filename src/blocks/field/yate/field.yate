module "xb-field"
import "xblocks"

match .* block-class {
    if (name() == 'disabled' && boolean(.)) {
        @class += " is-disabled"
    }

    if (name() == 'autosize' && boolean(.)) {
        @class += " xb-field_autosize"
    }

    if (name() == 'size') {
        @class += " xb-field_size_{.}"
    }
}

match / xb-field {
<xb-field>
    apply .attrs.* xb-core-attrs
</xb-field>
}

match /[.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label || .attrs.autosize] template {
controllerAttrs = [
    'name'
    'disabled'
    'placeholder'
]

<label class="xb-field_complex">
    apply .attrs.* block-class

    if (.attrs.label) {
        <xb-link href="#" type="input">
            apply .attrs.label.attrs.* xb-core-attrs
            .attrs.label.content
        </xb-link>
    }

    if (.attrs.prefix) {
        <span class="xb-field_complex__left">
            .attrs.prefix
        </span>
    }

    if (.attrs.postfix) {
        <span class="xb-field_complex__right">
            .attrs.postfix
        </span>
    }

    if (.attrs.reset) {
        <span class="xb-field_complex__reset js-reset"></span>
    }

    <span class="xb-field_complex__content">
        if (.attrs.multiline) {
            <textarea class="xb-field_complex__controller">
                apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
                    'rows'
                    'cols'
                ]))
                .attrs.value
            </textarea>
        } else {
            <input class="xb-field_complex__controller" type="text">
                apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
                    'value'
                ]))
            </input>
        }
        <span class="xb-field_complex__view">&nbsp;</span>
    </span>
</label>
}

match /[!(.attrs.postfix || .attrs.prefix || .attrs.reset || .attrs.label || .attrs.autosize)] template {
controllerAttrs = [
    'name'
    'disabled'
    'placeholder'
]


if (.attrs.multiline) {
    <textarea class="xb-field_simple">
        apply .attrs.* block-class
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'rows'
            'cols'
        ]))
        .attrs.value
    </textarea>
} else {
    <input class="xb-field_simple" type="text">
        apply .attrs.* block-class
        apply .attrs.* xb-core-attrs (xb-concat-array(controllerAttrs, [
            'value'
        ]))
    </input>
}
}
