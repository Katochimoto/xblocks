module "xblocks"

external scalar xb-escape(scalar)
external boolean xb-modernizr(scalar)
external boolean xb-inarray(scalar, array)
external boolean xb-empty-array(array)
external array xb-concat-array(array, array)

match .* xb-core-attrs (array contains, ignore) {
    if (xb-empty-array(contains)) {
        apply . xb-core-attrs__common

    } else if (ignore) {
        apply .[!xb-inarray(name(), contains)] xb-core-attrs__common

    } else {
        apply .[xb-inarray(name(), contains)] xb-core-attrs__common
    }
}

match .* xb-core-attrs__common {
    value = scalar(.)
    boolAttrs = [
        'disabled'
        'multiple'
        'checked'
    ]

    if (xb-inarray(name(), boolAttrs)) {
        if (value == true()) {
            @{name()} = name()
        }

    } else if (name() == 'class') {
        @class += (
            ' '
            value
        )

    } else {
        @{name()} = value
    }
}

match .* xb-core-content (escape = '') {
    <content>
        if (!xb-modernizr('createshadowroot')) {
            if (boolean(escape)) {
                .content
            } else {
                html(.content)
            }
        }
    </content>
}
